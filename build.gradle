plugins {
    id 'net.fabricmc.fabric-loom' version "${fabric_loom_version}"
    id "java-library"
    id "idea"
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = "_custom_${project.archives_base_name}-${project.minecraft_version}-fabric"
}

loom {
    runs {
        client {
            programArg "--username"
            programArg "DevPlayer"
            programArg "--uuid"
            programArg "00000000-0000-0000-0000-000000000000"
            ideConfigGenerated = true
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    implementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    implementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    implementation "org.jetbrains:annotations:latest.release"
}

// modmenu
repositories { maven { url = "https://maven.terraformersmc.com/" }}
dependencies { localRuntime ( "com.terraformersmc:modmenu:${project.modmenu_version}" )}

// mixin extras
if (file("src/main/resources/${project.archives_base_name}.mixins.json").exists()) {
    repositories { maven { url = "https://jitpack.io" }}
    dependencies { implementation ( annotationProcessor( "io.github.llamalad7:mixinextras-fabric:${project.mixinextras_version}" ))}
}

// build deps
if (file("build-dependencies.gradle").exists()) {
    apply from: "build-dependencies.gradle"
}

// runtime deps
if (file("build-runtime.gradle").exists()) {
    apply from: "build-runtime.gradle"
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(25))
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    it.options.release.set(25)
    it.options.encoding = "UTF-8"
}

processResources {
    def stringProperties = providers.gradlePropertiesPrefixedBy('').get()
    stringProperties.each { key, value -> inputs.property(key, value) }
    filesMatching(['fabric.mod.json', 'pack.mcmeta', 'en_us.json']) {
        filter { line -> line.contains('"$schema":') ? null : line }
        filter { line -> line.startsWith('/*') ? null : line }
        filter { line -> line.trim().isEmpty() ? null : line }
        expand(stringProperties) { escapeBackslash = true }
    }
}

jar {
    manifest {
        attributes(
                [
                        "Specification-Title"     : mod_id,
                        "Specification-Vendor"    : mod_authors,
                        "Specification-Version"   : "1",
                        "Implementation-Title"    : mod_name,
                        "Implementation-Version"  : mod_version,
                        "Implementation-Vendor"   : mod_authors,
                        "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
                ]
        )
    }
}